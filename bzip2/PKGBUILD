# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=dc-bzip2
pkgver=1.0.6
pkgrel=1
pkgdesc="A high-quality data compression program"
arch=('any')
license=('custom')
url="http://www.bzip.org"
options=(!strip libtool staticlibs)
source=(http://http.debian.net/debian/pool/main/b/bzip2/bzip2_$pkgver.orig.tar.bz2)
sha256sums=('d70a9ccd8bdf47e302d96c69fecd54925f45d9c7b966bb4ef5f56b770960afa7')
makedepends=('dc-pkg-config')
groups=('dc-portlibs')

build() {
  source /opt/pacbrew/dc/dc-env

  make -C "$srcdir/bzip2-$pkgver" \
    CC=${CC} \
    AR=${AR} \
    RANLIB=${RANLIB} \
    CPPFLAGS="${CPPFLAGS}" \
    CFLAGS="-D_FILE_OFFSET_BITS=64 -Winline ${CFLAGS}" libbz2.a
}

package() {
  source /opt/pacbrew/dc/dc-env
  install -Dm 644 "$srcdir/bzip2-$pkgver"/bzlib.h -t "${pkgdir}/${DC_PREFIX_INCLUDE}"
  install -Dm 644 "$srcdir/bzip2-$pkgver"/libbz2.a -t "${pkgdir}/${DC_PREFIX_LIB}"
}
